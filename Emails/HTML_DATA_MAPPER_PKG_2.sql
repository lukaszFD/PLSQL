CREATE OR REPLACE PACKAGE BODY HTML_DATA_MAPPER_PKG AS

    PROCEDURE POPULATE_TEMPLATE_WITH_NON_COMPLIANT_SUMMARY IS
        CURSOR C_USERS IS
            SELECT RECIPIENT, NAME, NON_COMPLIANT_COUNT, DEADLINE
            FROM VW_NON_COMPLIANT_ACCOUNTS;

        V_TEMPLATE_ID NUMBER;
        V_TEMPLATE_BODY CLOB;
        V_PERSONALIZED_BODY CLOB;
        V_EMAIL_ID NUMBER;
    BEGIN
        SELECT ID, BODY INTO V_TEMPLATE_ID, V_TEMPLATE_BODY 
        FROM EMAIL_TEMPLATES 
        WHERE NAME = 'NON_COMPLIANT_ALERT';

        FOR R IN C_USERS LOOP
            V_PERSONALIZED_BODY := REPLACE(V_TEMPLATE_BODY, '{{NAME}}', R.NAME);
            V_PERSONALIZED_BODY := REPLACE(V_PERSONALIZED_BODY, '{{NON_COMPLIANT_COUNT}}', R.NON_COMPLIANT_COUNT);
            V_PERSONALIZED_BODY := REPLACE(V_PERSONALIZED_BODY, '{{DEADLINE}}', TO_CHAR(R.DEADLINE, 'YYYY-MM-DD'));

            INSERT INTO EMAIL_READY_TO_SEND (RECIPIENT, SUBJECT, TEMPLATE_ID, BODY)
            VALUES (R.RECIPIENT, 'ACTION REQUIRED: NON-COMPLIANT ACCOUNTS', V_TEMPLATE_ID, V_PERSONALIZED_BODY)
            RETURNING ID INTO V_EMAIL_ID;

            INSERT INTO EMAIL_QUEUE (EMAIL_ID) VALUES (V_EMAIL_ID);
        END LOOP;

        COMMIT;
    END POPULATE_TEMPLATE_WITH_NON_COMPLIANT_SUMMARY;


    PROCEDURE POPULATE_TEMPLATE_WITH_ACCOUNT_LIST IS
        CURSOR C_USERS IS
            SELECT DISTINCT RECIPIENT, NAME
            FROM VW_NON_COMPLIANT_ACCOUNTS;

        V_TEMPLATE_ID NUMBER;
        V_TEMPLATE_BODY CLOB;
        V_PERSONALIZED_BODY CLOB;
        V_ACCOUNTS_LIST CLOB;
        V_EMAIL_ID NUMBER;
    BEGIN
        SELECT ID, BODY INTO V_TEMPLATE_ID, V_TEMPLATE_BODY 
        FROM EMAIL_TEMPLATES 
        WHERE NAME = 'NON_COMPLIANT_ACCOUNTS_LIST';

        FOR R IN C_USERS LOOP
            V_ACCOUNTS_LIST := '<ul>';
            FOR ACC IN (SELECT ACCOUNT_NAME, ACCOUNT_TYPE 
                        FROM VW_NON_COMPLIANT_ACCOUNTS
                        WHERE RECIPIENT = R.RECIPIENT) LOOP
                V_ACCOUNTS_LIST := V_ACCOUNTS_LIST || 
                    '<li>' || ACC.ACCOUNT_NAME || ' (' || ACC.ACCOUNT_TYPE || ')</li>';
            END LOOP;
            V_ACCOUNTS_LIST := V_ACCOUNTS_LIST || '</ul>';

            V_PERSONALIZED_BODY := REPLACE(V_TEMPLATE_BODY, '{{NAME}}', R.NAME);
            V_PERSONALIZED_BODY := REPLACE(V_PERSONALIZED_BODY, '{{ACCOUNT_LIST}}', V_ACCOUNTS_LIST);

            INSERT INTO EMAIL_READY_TO_SEND (RECIPIENT, SUBJECT, TEMPLATE_ID, BODY)
            VALUES (R.RECIPIENT, 'ACTION REQUIRED: NON-COMPLIANT ACCOUNTS LIST', V_TEMPLATE_ID, V_PERSONALIZED_BODY)
            RETURNING ID INTO V_EMAIL_ID;

            INSERT INTO EMAIL_QUEUE (EMAIL_ID) VALUES (V_EMAIL_ID);
        END LOOP;

        COMMIT;
    END POPULATE_TEMPLATE_WITH_ACCOUNT_LIST;

END HTML_DATA_MAPPER_PKG;
/